# Snipe

{

#DEFINES#
id: "snipe_playable",
prototype: ["player_controlled"],
editor_info: { category: "Characters" },
solid_dimensions: ["common", "~enemy"],
collide_dimensions: ["common","~booster","~enemy"],
vehicle: false,
has_feet: false,
feet_width: 0,
hitpoints: 30,
mass: 5,
is_human: true,
friction: 0,
friction_in_air: 0,
tracktion: 0,
zorder: "@include data/zorder.cfg:player",

properties: {
	team: "'snipe'",
	turn_speed: "int :: 120",
	cruise_speed: "int :: if(ctrl_left, 0, 5)",
	air_resistance: "decimal :: if(ctrl_left, 0.005, 0.001)",
	boost_amount: "int :: 200",
	
	camera_offset: "int :: 50",
	recent_x_pos: { type: "[int]", init: "range(8)" }, # Frames to make camera lag by
	camera_lag: "size(recent_x_pos)",
	set_x_pos: "set(recent_x_pos, recent_x_pos[1:camera_lag+1] + [me.midpoint_x])",
	init_x_pos: "set(recent_x_pos, map( range(camera_lag), left_bound + camera_offset))",
	last_x_pos: "recent_x_pos[0]",
	
	posthit_invicibility_period: "50",
	crash_slowdown: "int :: 2", // Velocity is divided by this when hitting something
		
	upper_bound: "level.dimensions[1]",	
	lower_bound: "level.dimensions[3]",	
	left_bound: "level.dimensions[0]",	
	right_bound: "level.dimensions[2]",
	
	handle_special_damage_response: "def(obj hittable|null collide_with) -> commands 
		if(collide_with != null and collide_with.team = 'boost', add(velocity_x, boost_amount),
		   collide_with != null and collide_with.team = 'evil' and not is_invincible_posthit,
		   	[set(velocity_x, velocity_x/2), add(hitpoints, 1)])",
		
	keep_from_leaving_y_range: "[
			if(me.midpoint_y < upper_bound,
				set(me.midpoint_y, upper_bound)),
			if(me.midpoint_y > lower_bound,
				set(me.midpoint_y, lower_bound))
			]",	
	keep_from_leaving_x_range: "
			if(me.midpoint_x + 800 - camera_offset > right_bound, [
				init_x_pos,
				set(level.camera_position, [(left_bound + camera_offset), me.midpoint_y]),
				set(me.midpoint_x, left_bound + camera_offset)
			])",
},

#ANIMATION HANDLES#
on_create: "[animation('stand'), set(facing, 1), init_x_pos]",
on_process_stand: "[set(time_in_animation_delta, max(velocity_x/750,1))
    ]",
    
on_end_stand_anim: "animation('stand')",

timer_frequency: 2,
on_timer: "[ //Handle ending of invincibility.
    if((level.cycle - time_last_hit)/2 <= 50 and (level.cycle - time_last_hit)/2 > 1,
        set(solid_dimensions_in, ['common','~booster','~enemy'])),
    if(not is_invincible_posthit, set(alpha,255), set(solid_dimensions_in, [])),
]",

on_process: "[
			set(velocity_x, velocity_x/velocity_x^air_resistance),
			set(velocity_y, velocity_y/2),
			set(accel_y, accel_y/1.3),
			
			//if(ctrl_up, add(velocity_y, -turn_speed)),
			//if(ctrl_down, add(velocity_y, turn_speed)),
			
			debug(velocity_x),
			
			add(velocity_x, cruise_speed),
			
			add(accel_y, if(
				ctrl_up and ctrl_down, 0,
				ctrl_up, -turn_speed,
				ctrl_down, turn_speed,
				0) ),
			
			keep_from_leaving_y_range,
			keep_from_leaving_x_range,
			
			set(level.camera_position, [last_x_pos - camera_offset, me.midpoint_y]),
			set_x_pos
		]",

#ANIMATIONS#
animation: [
	{
		id: "stand",
		body_area: "all",
		image: "characters/snipe.png",
		rect: [3, 3, 34, 34],
		solid_area: [12,12,21,21],
		frames: 3,
		frames_per_row: 8,
		pad: 1,
		duration: 5,
	},
],

}
