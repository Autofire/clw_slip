#Originates from Frogatto

# This prototype is for all the game-mechanics that have nothing to do with how a player moves, but which track stuff to do with things like key input, score tracking, events, achievements, inventory, etc.  This stuff should be fairly portable across game genre types.
# This prototype is the one declared in module.cfg as "the" playable character object type in frogatto, which all playable characters must inherit from.  The purpose of this is to allow us to reference functions specific to the character in levels (like recording game events) without having to do a type declaration on every single line.
# Also, this prototype must avoid any sort of physics/gameplay interaction with anything in the game, because it is also used for menu controllers and such.

{
id: "player_controlled",
solid_dimensions: [],
is_human: true,
prototype: ["hittable"],
collide_dimensions: [],

properties: {
	help: "string <- q{\nDebug Console Help:\nThis is the debug console. Commands, which are written in FFL, are run on the\nselected object. For example, you just ran the help command on our hero. Some\nother commands you could try might be 'set(velocity_y, -5000)', or 'add(hp,5)'.To change the target of your actions in the console, click on the desired\nobject. There is a lot more you can do with FFL, so have a look at the\ndocumentation on our wiki, https://github.com/frogatto/frogatto/wiki/. If you\nget stuck, www.frogatto.com has a friendly forum where you can seek advice.\nPress ctrl-d to close the console.\n}",
	
	team: "'player'",

	store_event_add: "def(string event_name, int addend) -> commands
		set(stored_events, zip(stored_events, {(event_name): addend}, a+b))
		asserting event_name != '' | {message:'Attempting to add a stored event, but the event name given is an empty string.', name:event_name, addend:addend}",
	
	store_event: "def(string event_name, int value) -> commands
		add(stored_events, {(event_name): value})
		asserting event_name != '' | {message:'Attempting to store an event, but the event name given is an empty string.', name:event_name, value:value}",
	
	get_stored_event: "def(string event_name) -> string|int|null (stored_events[event_name])",
	stored_events: {type: "{string -> int}", default: {"started_game" : 1}},


		//this gets used for stuff like event storage, but only for when it needs a non-integer type.  Don't ever use this unless you really need it (i.e. you need to store arrays of stuff, etc), because it skips around type checking.
	custom_storage: { type: "{string -> any}", default: {} },

	is_demo_version: "bool :: false",


# -- Camera stuff -- #	
	camera_offset: "int :: 50",
	recent_x_pos: { type: "[int]", init: "range(8)" }, # Frames to make camera lag by
	camera_lag: "size(recent_x_pos)",
	set_x_pos: "set(recent_x_pos, recent_x_pos[1:camera_lag+1] + [me.midpoint_x])",
	init_x_pos: "set(recent_x_pos, map( range(camera_lag), left_bound + camera_offset))",
	last_x_pos: "recent_x_pos[0]",
	
	keep_from_leaving_x_range: "commands :: 
		if(me.midpoint_x + 800 - camera_offset > right_bound, [
			init_x_pos,
			set(level.camera_position, [(left_bound + camera_offset), me.midpoint_y]),
			set(me.midpoint_x, left_bound + camera_offset)
	])",

#-------------------------- vars --------------------------#
		#-- general --#
	heal: "def(int amount) -> commands add(me.hitpoints, int <- min(amount, level.player.max_hitpoints-level.player.hitpoints))", //There is no equivalent mana function because mana, not being a built-in, has been able to have a setter added to it that does the same thing.

},

}
